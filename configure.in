dnl Process this file with autoconf to produce a configure script.
AC_INIT(app/colormaps.c)

dnl Specify a header configuration file
AC_CONFIG_HEADER(config.h)

dnl /bin/rm -f ./config.cache

dnl Make sure the `CFLAGS' variable is empty. This environment
dnl variable affects some of the tests because it affects the
dnl options during compiling. On some machines it can have a 
dnl negative affect.
CFLAGS=""

dnl Determine which C compiler is being used
AC_PROG_CC
AC_PROG_MAKE_SET

dnl Find the X11 include and library directories
AC_PATH_XTRA

echo $X_CFLAGS
echo $X_LIBS
echo $X_EXTRA_LIBS
echo $X_PRE_LIBS

if test "x$motif_includes" != x; then
  X_CFLAGS="$X_CFLAGS -I$motif_includes"
fi

X_LDFLAGS="-L$x_libraries"
if test "x$motif_libraries" != x; then
  X_LDFLAGS="$X_LIBS -L$motif_libraries"
fi

AC_SUBST(X_CFLAGS)
AC_SUBST(X_LDFLAGS)

dnl Make sure Motif is available
AC_CHECK_LIB(Xm, XmGetPixmap, LIBS="-lXm", no_motif=yes, -lXt $X_PRE_LIBS -lX11 $X_LDFLAGS)

dnl Add Xt to the list of libraries
LIBS="$LIBS -lXt $X_PRE_LIBS"

dnl Check for shared memory
AC_CHECK_HEADER(sys/ipc.h, AC_DEFINE(HAVE_IPC_H), no_sys_ipc=yes)
AC_CHECK_HEADER(sys/shm.h, AC_DEFINE(HAVE_SHM_H), no_sys_shm=yes)

dnl Check for the X shared memory extension header file
AC_MSG_CHECKING(X11/extensions/XShm.h)
if eval "test -f $x_includes/X11/extensions/XShm.h"; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_XSHM_H)
else
  AC_MSG_RESULT(no)
  no_xshm=yes
fi

dnl Check for the X extension library which contains the X shared memory functions
AC_CHECK_LIB(Xext, XShmAttach, LIBS="$LIBS -lXext", no_xext_lib=yes, -lX11 $X_LDFLAGS)

dnl Save the current "X" libraries and reset LIBS
SAVE_LIBS="$LIBS -lX11 $X_EXTRA_LIBS -lm"
LIBS=""

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME

AC_CHECK_HEADER(sys/time.h, AC_DEFINE(HAVE_SYS_TIME_H))
AC_CHECK_HEADER(unistd.h, AC_DEFINE(HAVE_UNISTD_H))

AC_TYPE_PID_T
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday strdup)

dnl Add the "X" libraries back into the LIBS variable
LIBS="$SAVE_LIBS $LIBS"

CFLAGS="-g -Wall"

dnl If we are using gcc then turn on strict ansi compilation
dnl if test "x$GCC" = xyes; then
dnl  CFLAGS="$CFLAGS -ansi -pedantic"
dnl fi

dnl Output the Makefiles
AC_OUTPUT(Makefile app/Makefile)

if test "x$no_x" = xyes; then
  config_problem=yes
  AC_MSG_WARN(could not find X11 header files)
fi

if test "x$no_motif" = xyes; then
  config_problem=yes
  AC_MSG_WARN(could not find Motif library)
fi

if test "x$no_sys_ipc" = xyes; then
  config_problem=yes
  AC_MSG_WARN(could not find sys/ipc.h header file)
fi

if test "x$no_sys_shm" = xyes; then
  config_problem=yes
  AC_MSG_WARN(could not find sys/shm.h header file)
fi

if test "x$no_xshm" = xyes; then
  config_problem=yes
  AC_MSG_WARN(could not find X11/extensions/XShm.h header file)
fi

if test "x$no_xext_lib" = xyes; then
  config_problem=yes
  AC_MSG_WARN(could not find the libXext library)
fi

if test "x$config_problem" = xyes; then
  AC_MSG_WARN(There was a problem with automatic configuration.)
  AC_MSG_WARN(Read the INSTALL file for trouble-shooting tips.)
fi
